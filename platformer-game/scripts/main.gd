extends Node2D

@onready var score_label: Label = $CanvasLayer/Panel/ScoreLabel
@onready var fade: ColorRect = $CanvasLayer/Fade

var level: int = 1 
var score = 0
var current_level_root: Node = null


func _ready() -> void:
	fade.modulate.a = 1.0
	current_level_root = get_node("LevelRoot")
	await _load_level(level, true)
	update_score()

func _load_level(level_number: int, first_load: bool) -> void:
	if not first_load:
		await _fade(1.0)
	
	if current_level_root:
		current_level_root.queue_free()
		
	var level_path = "res://scene/levels/level%s.tscn" % level_number
	current_level_root = load(level_path).instantiate()
	add_child(current_level_root)
	current_level_root.name = "LevelRoot"
	_setup_level(current_level_root)
	
	await _fade(0.0)

func _setup_level(level_root: Node) -> void:
	var exit = level_root.get_node_or_null("Exit")
	if exit:
		exit.body_entered.connect(_on_exit_body_entered)
	
	var enemies = level_root.get_node_or_null("Enemies")
	if enemies:
		for enemy in enemies.get_children():
			enemy.player_died.connect(_on_player_died)


func _on_exit_body_entered(body: Node2D) -> void:
	if body.name == "Player":
		level += 1
		print(level)
		body.can_move = false
		await _load_level(level, false)

func _on_player_died(body):
	await _load_level(level, false) 
	body.die()


func add_score(amount: int) -> void:
	score += amount
	update_score()


func update_score() -> void:
	score_label.text = "Score: " + str(score)

func _fade(to_alpha: float) -> void:
	var tween := create_tween()
	tween.tween_property(fade, "modulate:a", to_alpha, 1.5)
	await tween.finished
